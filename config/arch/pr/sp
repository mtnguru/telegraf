
# This Starlark processor is used when loading Sparkplug B protobuf #
# messages into InfluxDB.  The data source is a Opto22 Groov EPIC controller.  
# This processor does the following:
#   - Resolves the metric name using a numeric alias
#     When the EPIC MQTT client is started it sends a DBIRTH message 
#     that lists all metrics configured on the controller and includes 
#     a sequential numeric alias to reference it by.  
#     This processor stores that information in the array states["aliases"].

#     When subsequent DDATA messages are published, the numeric alias is 
#     used to find the stored metric name in the array states["aliases"].
#  - Splits the MQTT topic into 5 fields which are used as tags in InfluxDB.
#  - Splits the metric name into 5 fields which are used as tags in InfluxDB.
#
# NOTE: The initial input comes from an MQTT broker in Sparkplug_b format. 
#       This input is not human readable and is not posted here.
#       Instead the example input below is after the xpath_protobuf parser has parsed it
#
# Example Input:
# edge,host=firefly,topic=spBv1.0/SF/DDATA/epiclc/Exp501 type=9i,alias=10i,value=24.195923 1626358322348000000
# edge,host=firefly,topic=spBv1.0/SF/DDATA/epiclc/Exp501 value=24.176422,type=9i,alias=10i 1626358323350000128
# edge,host=firefly,topic=spBv1.0/SF/NDEATH/epiclc type=4i,value=0,name="bdSeq" 1626358065824999936
# edge,host=firefly,topic=spBv1.0/SF/DBIRTH/epiclc/Exp501 type=9i,alias=9i,name="Strategy/IO/Ch_TC_Right_C" 1626358327953999872
# edge,host=firefly,topic=spBv1.0/SF/DBIRTH/epiclc/Exp501 type=9i,alias=10i,value=24.16632,name="Strategy/IO/Ch_TC_Top_C" 1626358327953999872
# edge,host=firefly,topic=spBv1.0/SF/DDATA/epiclc/Exp501 type=9i,alias=10i,value=24.203125 1626358329036000000
# edge,host=firefly,topic=spBv1.0/SF/DDATA/epiclc/Exp501 type=9i,alias=9i,value=24.154877 1626358330036000000
# edge,host=firefly,topic=spBv1.0/SF/DDATA/epiclc/Exp501 alias=10i,value=24.183044,type=9i 1626358331036000000
#
# Example Output:
# edge,EdgeID=epiclc,Experiment=Exp501,Reactor=SF type=9i,alias=9i,name="Strategy/IO/Ch_TC_Right_C" 1626358328000000000
# edge,EdgeID=epiclc,Experiment=Exp501,Reactor=SF type=9i,alias=10i,value=24.16632,name="Strategy/IO/Ch_TC_Top_C" 1626358328000000000
# edge,Component=Ch,Datatype=IO,Device=TC,EdgeID=epiclc,Experiment=Exp501,Metric=Ch_TC_Top_C,Position=Top,Reactor=SF,Source=Strategy,Units=C value=24.203125 1626358329000000000
# edge,Component=Ch,Datatype=IO,Device=TC,EdgeID=epiclc,Experiment=Exp501,Metric=Ch_TC_Right_C,Position=Right,Reactor=SF,Source=Strategy,Units=C value=24.154877 1626358330000000000
# edge,Component=Ch,Datatype=IO,Device=TC,EdgeID=epiclc,Experiment=Exp501,Metric=Ch_TC_Top_C,Position=Top,Reactor=SF,Source=Strategy,Units=C value=24.183044 1626358331000000000

#############################################
# The following is the telegraf.conf used when calling this processor

# [[inputs.mqtt_consumer]]
#   servers = ["tcp://your_server:1883"]
#   qos = 0
#   connection_timeout = "30s"
#   topics = ["spBv1.0/#"]
#   persistent_session = false
#   client_id = ""
#   username = "your username"
#   password = "your password"
# 
#   # Sparkplug protobuf configuration
#   data_format = "xpath_protobuf"
#    
#   # URL of sparkplug protobuf prototype
#   xpath_protobuf_type = "org.eclipse.tahu.protobuf.Payload"
#    
#   # Location of sparkplug_b.proto file
#   xpath_protobuf_file = "/apps/telegraf/config/sparkplug_b.proto"
# 
#   [[inputs.mqtt_consumer.xpath_protobuf]]
#     metric_selection = "metrics[not(template_value)]"
#     metric_name = "concat('edge', substring-after(name, ' '))"
#     timestamp = "timestamp"
#     timestamp_format = "unix_ms"
#     [inputs.mqtt_consumer.xpath_protobuf.tags]
#       name = "substring-after(name, ' ')"
#     [inputs.mqtt_consumer.xpath_protobuf.fields_int]
#       type = "datatype"
#       alias = "alias"
#     [inputs.mqtt_consumer.xpath_protobuf.fields]
#       # A metric value must be numeric
#       value = "number((int_value | long_value | float_value | double_value | boolean_value))"
#       name = "name"
# 
# # Starlark processor
# [[processors.starlark]]
#   script = "sparkplug.star"
# 
#   # Define constants used in sparkplug.star
#   [processors.starlark.constants]     
# 
#     # Provide alternate names for the 5 sparkplug topic fields.  
#     # The topic contains 5 fields separated by the '/' character.  
#     # Define the tag name for each of these fields.
#     MSG_FORMAT = "false"        #0
#     GROUP_ID   = "reactor"      #1
#     MSG_TYPE   = "false"        #2
#     EDGE_ID    = "edgeid"       #3
#     DEVICE_ID  = "experiment"   #4
#
 
########### Begin sparkplug.star script

load("logging.star", "log")

state = {
    "aliases":    dict(),
    "devices":    dict(),
    "unresolved": list()
}

BIRTH_TAG  = "BIRTH/"
DATA_TAG   = "DATA/"

MAX_UNRESOLVED = 3

MSG_FORMAT = "false"        #0
GROUP_ID   = "Reactor"      #1
MSG_TYPE   = "false"        #2
EDGE_ID    = "EdgeID"       #3
DEVICE_ID  = "Experiment"   #4

def extractTopicTags(metric):
    msg_format   = 'xx'
    groupid      = 'xx'
    msg_type     = 'xx'
    edgeid       = 'xx'
    deviceid     = 'xx'

    topic = metric.tags.get("topic", "");
    fields = topic.split("/");
    nfields = len(fields)
    if nfields > 0:
      msg_format = fields[0]
      if nfields > 1:
        groupid = fields[1]
        if nfields > 2:
          msg_type = fields[2]
          if nfields > 3:
            edgeid = fields[3]
            if nfields > 4:
              deviceid = fields[4]
    return [msg_format, groupid, msg_type, edgeid, deviceid]
   
                
def createTopicTags(metric, topicFields):
    # Remove topic and host tags - not needed
    metric.tags.pop("topic")
    metric.tags.pop("host")

    if MSG_FORMAT != "false": metric.tags[MSG_FORMAT] = topicFields[0] 
    if GROUP_ID   != "false": metric.tags[GROUP_ID]   = topicFields[1] 
    if MSG_TYPE   != "false": metric.tags[MSG_TYPE]   = topicFields[2] 
    if EDGE_ID    != "false": metric.tags[EDGE_ID]    = topicFields[3]
    if DEVICE_ID  != "false": metric.tags[DEVICE_ID]  = topicFields[4]


def createNameTags(metric,name):
    # Remove type and alias from metric.fields - They are not useful
    metric.fields.pop("type")
    metric.fields.pop("alias")

    # The Groov EPIC metric names are comprised of 3 fields
    #   source, datatype, and metric name
    # Extract these fields and include them as tags.
    fields = name.split('/')
    nfields = len(fields)
    if nfields > 0: 
        metric.tags["Source"] = fields[0]
        if nfields > 1:
            # Shorten excessively long datatype strings
            metric.tags["Datatype"] = fields[1]
            if nfields > 2: 
                metric.tags["Metric"] = fields[2]

                # OPTIONAL
                #
                # By using underscore characters the metric name can be further
                # divided into additional tags.  
                # How this is defined is site specific.  
                # Customize this as you wish

                # The following demonstrates dividing the metric name into 4 new tags
                # A metric name can have from 2-5 underscore separated fields 
                #
                # If there is only one field then the only tag created is 'metric'
                #
                # The last field is optional and is filled before fields 2, 3 and 4
                # Fields 2, 3, and 4 (device, position, composition) are optional
                #    component_device_postion_composition_units
                #
                # Ex:  FuelTank1_C                    (2 fields) 
                #         Component     FuelTank1   
                #         Units         C
                #      FuelTank1_TC_Outlet_C          (3 fields)           
                #         Component     FuelTank1   
                #         Device        TC
                #         Position      Outlet
                #         Units         C
                #      FuelTank1_TC_Outlet_Premium_C  (5 fields) 
                #         Component     FuelTank1   
                #         Device        TC  
                #         Position      Outlet
                #         Composition   Premium   
                #         Units         C

                # Split the metric name into fields using '_' 
                sfields = fields[2].split('_')
                nf = len(sfields)
                if nf > 1:
                    metric.tags["Component"] = sfields[0]
                    metric.tags["Units"] = sfields[nf-1]
                if nf > 2:
                    metric.tags["Device"] = sfields[1]
                if nf > 3:
                    metric.tags["Position"] = sfields[2]
                if nf > 4:
                    metric.tags["Composition"] = sfields[3]

def apply(metric):
    output = metric

    log.debug("apply metric: {}".format(metric))

    topic = metric.tags.get("topic", "")
    topicFields = extractTopicTags(metric)
    edgeid = topicFields[3]      # Sparkplug spec specifies 4th field as edgeid

    # Split the topic into fields and assign to variables
    # Determine if the message is of type birth and if so add it to the "devices" LUT.
    if BIRTH_TAG in topic:
        log.debug("    metric msg_type: {}    edgeid: {}   topic: {}".format(BIRTH_TAG, edgeid, topic))
        if "alias" in metric.fields and "name" in metric.fields:
            # Create the lookup-table using "${edgeid}/${alias}" as the key and "${name}" as value
            alias = metric.fields.get("alias")
            name = metric.fields.get("name")

            id = "{}/{}".format(edgeid,alias)
            log.debug("  --> setting alias: {}    name: {}   id: {}'".format(alias, name, id))
            state["aliases"][id] = name
            createTopicTags(metric, topicFields)

            # Try to resolve the unresolved if any
            if len(state["unresolved"]) > 0:
                # Filter out the matching metrics and keep the rest as unresolved
                log.debug("    unresolved")
                unresolved = [("{}/{}".format(edgeid, m.fields["alias"]), m) for m in state["unresolved"]]
                matching = [(mid, m) for mid, m in unresolved if mid == id]
                state["unresolved"] = [m for mid, m in unresolved if mid != id]

                log.debug("    found {} matching unresolved metrics".format(len(matching)))
                # Process the matching metrics and output - needs work
                # for mid, m in matching:
                #     createTopicTags(m,topicFields)
                #     createNameTags(m)
                # output = [m for _, m in matching] + [metric]

    elif DATA_TAG in topic:
        log.debug("    metric msg_type: {}    edgeid: {}   topic: {}".format(DATA_TAG, edgeid, topic))
        if "alias" in metric.fields:
            alias = metric.fields.get("alias")

            # Lookup the ID. If we know it, replace the name of the metric with the lookup value,
            # otherwise we need to keep the metric for resolving later. 
            # This can happen if the messages are out-of-order for some reason...
            id = "{}/{}".format(edgeid,alias)
            if id in state["aliases"]:
                name = state["aliases"][id]
                log.debug("    found alias: {}     name: {}".format(alias, name))
                createTopicTags(metric,topicFields)
                createNameTags(metric,name)
            else:
                # We want to hold the metric until we get the corresponding birth message
                log.debug("    id not found: {}".format(id))
                output = None
                if len(state["unresolved"]) >= MAX_UNRESOLVED:
                    log.warn("    metric overflow, trimming {}".format(len(state["unresolved"]) - MAX_UNRESOLVED+1))
                    # Release the unresolved metrics as raw and trim buffer
                    output = state["unresolved"][MAX_UNRESOLVED-1:]
                    state["unresolved"] = state["unresolved"][:MAX_UNRESOLVED-1]
                log.debug("    --> keeping metric")
                state["unresolved"].append(metric)

    return output

